apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30
    ndkVersion "22.1.7171670"

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 240451
        versionName "4.5.1"
        project.archivesBaseName = "ffmpeg-kit"
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ["../libs"]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            systemProperty 'enable.ffmpeg.kit.test.mode', 'true'
        }
    }
}

task javadoc(type: Javadoc) {
    title = 'FFmpegKit'
    destinationDir = file("${projectDir}/../../docs/android/javadoc")
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    source = android.sourceSets.main.java.srcDirs
    configurations.implementation.setCanBeResolved(true)
    classpath += configurations.implementation
}

def getVersionName = { ->
    return "4.5.1-1.3.LTS"
}

def getGroupId = { ->
    return "com.github.jmir1"
}


task sourceJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

publishing {
    publications {
        bar(MavenPublication) {
            groupId getGroupId()
            version getVersionName()
            artifact("$buildDir/outputs/aar/ffmpeg-kit-release.aar")
            artifact(tasks["sourceJar"])
        }
    }
}

dependencies {
    implementation 'com.arthenica:smart-exception-java:0.1.1'
    testImplementation "androidx.test.ext:junit:1.1.3"
    testImplementation "org.json:json:20201115"
}

if (System.properties.containsKey('releaseFFmpegKit')) {
    apply from: "${rootProject.projectDir}/../tools/release/android/release.template.gradle"
}
